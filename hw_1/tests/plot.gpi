#
# HEATMAP
#

reset
set terminal pngcairo enhanced font "Vera,20" size 1024, 768
set output "result_heatmap.png"

set xlabel "# Cells"
set ylabel "# Nets"
set cblabel "Time (s)"
unset key

set zdata time
set timefmt "%S"
set format z "%H:%M:%S"

set style fill transparent solid 0.8 noborder
set xrange [0:25000]
set yrange [0:1250000]

plot 'results.csv' u 1:2:(100*sqrt($3)):3 w circles lc palette, 'factorial.txt' w lines

#
# TIME VS CELLS
#

reset
set terminal pngcairo enhanced font "Vera,20" size 1024, 768
set output "result_cells.png"

set xlabel "# Cells"
set ylabel "Time (H:M:S)"
#unset key
set key left top

set ydata time
set timefmt "%S"
set format y "%H:%M:%S"

f(x) = a*x + b
fit f(x) '< sort -k 1 -n results.csv' u 1:3 via a, b
title_f(a,b) = sprintf('f(x) = %fx + %f', a, b)

plot '< sort -k 1 -n results.csv' u 1:3 w lines, f(x) t title_f(a,b) lw 4

#
# TIME VS NETS
#

reset
set terminal pngcairo enhanced font "Vera,20" size 1024, 768
set output "result_nets.png"

set xlabel "# Nets"
set ylabel "Time (H:M:S)"
#unset key

set ydata time
set timefmt "%S"
set format y "%H:%M:%S"

set xrange [0:1250000]

f(x) = a*x + b
fit f(x) '< sort -k 1 -n results.csv' u 2:3 via a, b
title_f(a,b) = sprintf('f(x) = %fx + %f', a, b)

plot '< sort -k 2 -n results.csv' u 2:3 w lines, f(x) t title_f(a,b) lw 4
